// Test basic interface functionality

// Basic interface definition
interface Named {
    name: string
}

// Record that structurally implements the Named interface
record Person {
    name: string,
    age: i32,
}

interface Hashable {
    func hash() -> i64
}

record User implements Hashable {
    id: i64,
    name: string,

    func hash() -> i64 {
        return self.id
    }
}

interface Serializable {
    func toJson() -> string
}

record Entity implements Hashable, Serializable {
    id: i64,
    name: string,

    func hash() -> i64 {
        return self.id
    }

    func toJson() -> string {
        return self.name
    }
}

interface Identifiable {
    id: i64
}

interface Persistable extends Identifiable {
    func save() -> bool
}

record Document implements Persistable {
    id: i64,
    content: string,

    func save() -> bool {
        return true
    }
}

interface Hashable2 {
    func hash() -> i64
}

func getHash<T: Hashable2>(x: T) -> i64 {
    return x.hash()
}

record Widget implements Hashable2 {
    id: i64,
    func hash() -> i64 { return self.id }
}

interface Predicate {
    func check(value: i32) -> bool
}

// Test that lambda can be coerced to functional interface
let isEvenPred: Predicate = (x: i32) -> bool => x % 2 == 0

// Default methods on interfaces
interface Comparable {
    func compare(other: i32) -> i32

    func lessThan(other: i32) -> bool {
        return self.compare(other) < 0
    }
}

record Score implements Comparable {
    value: i32,

    func compare(other: i32) -> i32 {
        return self.value - other
    }
}

tests {
    test "interface can be declared" {
        let person = Person { name: "Alice", age: 30 }
        assert(person.name == "Alice")
        assert(person.age == 30)
    }

    test "explicit implements" {
        let user = User { id: 42, name: "Bob" }
        assert(user.hash() == 42)
    }

    test "multiple interface implementation" {
        let entity = Entity { id: 123, name: "entity123" }
        assert(entity.hash() == 123)
        assert(entity.toJson() == "entity123")
    }

    test "interface extends" {
        let doc = Document { id: 1, content: "Hello" }
        assert(doc.id == 1)
        assert(doc.save() == true)
    }

    test "interface name as constraint" {
        let w = Widget { id: 99 }
        assert(getHash(w) == 99)
    }

    test "lambda satisfies functional interface" {
        // Direct call on functional interface works
        assert(isEvenPred(4) == true)
        assert(isEvenPred(3) == false)

        // Method call on functional interface (calls underlying lambda)
        assert(isEvenPred.check(4) == true)
        assert(isEvenPred.check(3) == false)
    }

    test "default methods from interface" {
        let a = Score { value: 10 }
        assert(a.compare(20) == -10)
        assert(a.lessThan(20) == true)
    }
}
